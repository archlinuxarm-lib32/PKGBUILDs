# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Levente Polyak <anthraxx@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=(lib32-zlib lib32-minizip)
epoch=1
pkgver=1.3.1
pkgrel=2
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'
arch=(x86_64)
license=(Zlib)
url="https://www.zlib.net/"
options=(
  staticlibs  # needed by binutils testsuite
  debug
)
source=(https://github.com/madler/zlib/releases/download/v$pkgver/zlib-$pkgver.tar.xz)
sha512sums=('1e8e70b362d64a233591906a1f50b59001db04ca14aaffad522198b04680be501736e7d536b4191e2f99767e7001ca486cd802362cca2be05d5d409b83ea732d'
            )
b2sums=('42d109223801a493de6d52e7343403d7fc3234a6ca816425fe41ac9c18019b01b93841acd28a235e99f2256a6a17f93624e96b2ddb58d588c8190a6bedb82910'
        )

prepare() {
  cd zlib-$pkgver/contrib/minizip
  cp Makefile Makefile.orig
  cp ../README.contrib readme.txt
  autoreconf -fiv
}

build() {
  export CC="armv7l-linux-gnueabihf-gcc"
  export CXX="armv7l-linux-gnueabihf-g++"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  CFLAGS=${CFLAGS/-mno-omit-leaf-frame-pointer/}
  export LD="armv7l-linux-gnueabihf-ld"
  export AS="armv7l-linux-gnueabihf-as"
  CXXFLAGS=${CXXFLAGS/-mno-omit-leaf-frame-pointer/}
  cd zlib-$pkgver

  CFLAGS+=" -ffat-lto-objects"
  ./configure --prefix=/usr \
    --libdir=/usr/lib32 \
    --includedir=/usr/include32
  make

  cd contrib/minizip
  ./configure --prefix=/usr --enable-static=no \
    --libdir=/usr/lib32 \
    --includedir=/usr/include32
  make
}

check() {
  make test -C zlib-$pkgver

  make -f Makefile.orig test -C zlib-$pkgver/contrib/minizip
}

package_lib32-zlib() {
  depends=(lib32-glibc)

  make install DESTDIR="$pkgdir" -C zlib-$pkgver
  rm -rf "$pkgdir"/usr/share/mano
  sed -i 's/includedir=${prefix}\/include/includedir=${prefix}\/include32/' "$pkgdir"/usr/lib32/pkgconfig/*
  install -D -m644 zlib-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

package_lib32-minizip() {
  pkgdesc='Mini zip and unzip based on zlib'
  depends=(
    lib32-glibc
    lib32-zlib
  )

  make install DESTDIR="$pkgdir" -C zlib-$pkgver/contrib/minizip
  rm -rf "$pkgdir"/usr/share/man
  install -D -m644 zlib-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"

  # https://github.com/madler/zlib/pull/229
  sed -i 's/includedir=${prefix}\/include/includedir=${prefix}\/include32/' "$pkgdir"/usr/lib32/pkgconfig/*
  rm "$pkgdir/usr/include32/minizip/crypt.h"
}

# vim: ts=2 sw=2 et:
