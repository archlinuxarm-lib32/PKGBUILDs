# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Judd <jvinet@zeroflux.org>

pkgname=lib32-bzip2
pkgver=1.0.8
pkgrel=6
pkgdesc="A high-quality data compression program"
arch=('x86_64')
license=('BSD')
url="https://sourceware.org/bzip2/"
depends=('lib32-glibc' 'sh')
source=(https://sourceware.org/pub/bzip2/bzip2-$pkgver.tar.gz{,.sig}
        bzip2.pc)
sha256sums=('ab5a03176ee106d3f0fa90e381da478ddae405918153cca248e682cd0c4a2269'
            'SKIP'
            '3b354bb7ffb864124c440da8b44f563f157dedc719f2f53adc214f908778ed25')
validpgpkeys=('EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A') # Mark Wielaard <mark@klomp.org>

prepare() {
  cd bzip2-$pkgver
  cp ../bzip2.pc bzip2.pc
  sed "s|@VERSION@|$pkgver|" -i bzip2.pc
}

build() {
  cd bzip2-$pkgver
  CFLAGS=${CFLAGS/-mno-omit-leaf-frame-pointer/}
  CXXFLAGS=${CXXFLAGS/-mno-omit-leaf-frame-pointer/}
  make -f Makefile-libbz2_so CC="armv7l-linux-gnueabihf-gcc $CFLAGS $CPPFLAGS $LDFLAGS" AS="armv7l-linux-gnueabihf-as"
  make libbz2.a CC="armv7l-linux-gnueabihf-gcc $CFLAGS $LDFLAGS" AS="armv7l-linux-gnueabihf-as"
}

check() {
  cd bzip2-$pkgver
  make test
}

package() {
  cd bzip2-$pkgver

  install -dm755 "$pkgdir"/usr/{lib32,include32}

  cp -a libbz2.so* "$pkgdir"/usr/lib32
  ln -s libbz2.so.$pkgver "$pkgdir"/usr/lib32/libbz2.so
  ln -s libbz2.so.$pkgver "$pkgdir"/usr/lib32/libbz2.so.1 # For compatibility with some other distros

  install -m644 bzlib.h "$pkgdir"/usr/include32/

  install -Dm644 bzip2.pc -t "$pkgdir"/usr/lib32/pkgconfig
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
}
