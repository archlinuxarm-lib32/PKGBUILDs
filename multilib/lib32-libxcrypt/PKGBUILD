# Maintainer: Christian Hesse <mail@eworm.de>

pkgbase=lib32-libxcrypt
pkgname=(lib32-libxcrypt lib32-libxcrypt-compat)
pkgver=4.4.38
pkgrel=1
pkgdesc='Modern library for one-way hashing of passwords (32-bit)'
arch=('x86_64')
url='https://github.com/besser82/libxcrypt/'
license=('LGPL')
depends=('lib32-glibc' 'libxcrypt')
makedepends=('lib32-gcc')
validpgpkeys=('678CE3FEE430311596DB8C16F52E98007594C21D') # Bj√∂rn 'besser82' Esser
source=("${url}/releases/download/v${pkgver}/libxcrypt-${pkgver}.tar.xz"{,.asc})
sha256sums=('80304b9c306ea799327f01d9a7549bdb28317789182631f1b54f4511b4206dd6'
            'SKIP')

build() {
  export CC="armv7l-linux-gnueabihf-gcc"
  export CXX="armv7l-linux-gnueabihf-g++"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export LD="armv7l-linux-gnueabihf-ld"
  export AS="armv7l-linux-gnueabihf-as"
  CFLAGS=${CFLAGS/-mno-omit-leaf-frame-pointer/}
  CXXFLAGS=${CXXFLAGS/-mno-omit-leaf-frame-pointer/}


  mkdir build-libxcrypt build-libxcrypt-compat
  cd "${srcdir}/build-libxcrypt/"
  "${srcdir}/libxcrypt-${pkgver}"/configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32 \
    --target=armv7l-linux-gnueabihf \
    --host=armv7l-linux-gnueabihf \
    --build=armv7l-linux-gnueabihf \
    --includedir=/usr/include32 \
    --disable-static \
    --enable-hashes=strong,glibc \
    --enable-obsolete-api=no \
    --disable-failure-tokens
  make 

  cd "${srcdir}/build-libxcrypt-compat/"
  "${srcdir}/libxcrypt-${pkgver}"/configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32 \
    --target=armv7l-linux-gnueabihf \
    --host=armv7l-linux-gnueabihf \
    --build=armv7l-linux-gnueabihf \
    --includedir=/usr/include32 \
    --disable-static \
    --enable-hashes=strong,glibc \
    --enable-obsolete-api=glibc \
    --disable-failure-tokens
  make 
}

package_lib32-libxcrypt() {
  cd build-libxcrypt/

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,share}
}

package_lib32-libxcrypt-compat() {
  pkgdesc='Modern library for one-way hashing of passwords - legacy API functions (32bit)'
  depends=('lib32-libxcrypt')
  
  cd build-libxcrypt-compat/
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{lib32/{lib*.so,pkgconfig},share}
}
