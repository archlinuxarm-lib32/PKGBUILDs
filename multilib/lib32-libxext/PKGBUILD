# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxext
pkgname=lib32-$_pkgbasename
pkgver=1.3.6
pkgrel=1
pkgdesc="X11 miscellaneous extensions library (32-bit)"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libx11' $_pkgbasename)
makedepends=('xorg-util-macros' 'lib32-gcc')
source=(${url}/releases/individual/lib/libXext-${pkgver}.tar.xz{,.sig})
sha512sums=('fd7693b5698cc4f5e80002a2cd4bd88e5d52c755c19a0417ee73029bd65f9d77b0969e765a1febc4d8fce1ba5dfb7278717f47be742e33d8fc5eb941e769792e'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd "${srcdir}/libXext-${pkgver}"

  export CC="armv7l-linux-gnueabihf-gcc"
  export CXX="armv7l-linux-gnueabihf-g++"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export LD="armv7l-linux-gnueabihf-ld"
  export AS="armv7l-linux-gnueabihf-as"
  CFLAGS=${CFLAGS/-mno-omit-leaf-frame-pointer/}
  CXXFLAGS=${CXXFLAGS/-mno-omit-leaf-frame-pointer/}


  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32 \
    --target=armv7l-linux-gnueabihf \
    --host=armv7l-linux-gnueabihf \
    --build=armv7l-linux-gnueabihf \
    --libdir=/usr/lib32 \
    --includedir=/usr/include32
  make
}

package() {
  cd "${srcdir}/libXext-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/share

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname" 
}
